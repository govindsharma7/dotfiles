"Vundle plugin and bundle initialization
set rtp+=~/.vim/vundle/
call vundle#rc()
"Github Bundles
Bundle 'benmills/vimux'
Bundle 'chriskempson/base16-vim'
Bundle 'corntrace/bufexplorer'
Bundle 'ervandew/supertab'
Bundle 'kien/ctrlp.vim'
Bundle 'Lokaltog/vim-easymotion'
Bundle 'marijnh/tern_for_vim'
Bundle 'mileszs/ack.vim'
Bundle 'nelstrom/vim-visual-star-search'
Bundle 'plasticboy/vim-markdown'
Bundle 'scrooloose/nerdcommenter'
Bundle 'scrooloose/nerdtree'
Bundle 'sjl/gundo.vim'
Bundle 'tpope/vim-fugitive'
Bundle 'tpope/vim-vividchalk'

"NOTE: This block was commented out in favor of 'set list'. Uncomment all
"executable lines in this block to restore tab/whitespace hilighting
"functionality.
"
"Whitespace matching: must appear before the colorscheme is set
"SUMMARY OF BEHAVIOR:
" 1) Apply to all buffers.
" 2) Match all tab characters at all times.
" 3) Inside insert mode, don't match trailing whitespace on the current line.
" 4) Outside insert mode, match all trailing whitespace on the current line.
"highlight ExtraWhitespace ctermbg=red guibg=red
"match ExtraWhitespace /\t\|\s\+$/
"if has("autocmd")
"  autocmd ColorScheme * highlight ExtraWhitespace ctermbg=red guibg=red
"  autocmd BufWinEnter * match ExtraWhitespace /\t\|\s\+$/
"  autocmd InsertEnter * match ExtraWhitespace /\t\|\s\+\%#\@<!$/
"  autocmd InsertLeave * match ExtraWhitespace /\t\|\s\+$/
"  autocmd BufWinLeave * call clearmatches()
"endif

if has("gui") "Instead of gui_running, in case :gui is run manually on *NIX
  "Font settings
  set guifont=DejaVu\ Sans\ Mono:h12
  "set noantialias
  set antialias

  "OS-specific settings.
  if has("unix")
    set linespace=-1
  else
    source $VIMRUNTIME/mswin.vim "Enable expected keyboard shortcuts for Windows - see :help :behave
    set keymodel-=stopsel "Make Visual mode work as expected when mswin.vim is sourced
  endif
  set guioptions=egmrt "Hide toolbar by default in MacVim
  "Found at <http://support.markedapp.com/kb/how-to-tips-and-tricks/marked-bonus-pack-scripts-commands-and-bundles>
  :nnoremap <leader>m :silent !open -a Marked.app '%:p'<cr> "Bind a shortcut for opening the current document in Marked.app on Mac OS X
  if has("transparency") "Background transparency is a MacVim-specific feature, so prevent errors in other vims
    set transparency=5 "Enable background transparency in MacVim
  endif
endif

"Disable splash screen/[I]ntro message
set shortmess+=I

set background=dark
let base16colorspace=256  " Access colors present in 256 colorspace (base16 color scheme)
colorscheme base16-tomorrow

set number
set nobackup
set nowritebackup
"set list "Show invisible characters by default

set softtabstop=2
set tabstop=2
set shiftwidth=2
set expandtab

set bs=2 "Allow backspace to work properly
set autoindent
set smartindent
set enc=utf-8
set nowrap
filetype on
filetype plugin on
filetype indent on "Enable automatic indentation based on detected filetype
syntax on

"Statusline
if exists("vimpager")
  set statusline=%<%F%h%m%r%h%w%y\ %{&ff}\ %{strftime(\"%c\",getftime(expand(\"%:p\")))}\ %=\ lin:%l\/%L\ col:%c%V\ %P
else
  set statusline=%<%F%h%m%r%h%w%y\ %{&ff}\ %{strftime(\"%c\",getftime(expand(\"%:p\")))}\ %{fugitive#statusline()}\ %=\ lin:%l\/%L\ col:%c%V\ %P
endif
set laststatus=2
let g:Powerline_symbols = 'fancy'

"Invisible (list) character colors
"highlight NonText guifg=#555555 "EOL
"highlight SpecialKey guifg=#555555 "Tab

"On Mac, map CMD-[ and CMD-] to indent while preserving any Visual mode selection as appropriate
nmap <D-[> <<
nmap <D-]> >>
vmap <D-[> <gv
vmap <D-]> >gv

":w!! will use sudo to save a non-writable file if you forgot to do 'sudo vim' by accident
cmap w!! %!sudo tee > /dev/null %

"Use the same symbols as TextMate for tabstops and EOLs
set listchars=tab:▸\ ,eol:¬

"Treat .md files as Markdown
au BufRead,BufNewFile *.md set filetype=markdown

"Super Retab
"Found at <http://vim.wikia.com/wiki/Super_retab>
" Space2Tab  Convert spaces to tabs, only in indents.
" Tab2Space  Convert tabs to spaces, only in indents.
" RetabIndent  Execute Space2Tab (if 'expandtab' is set), or Tab2Space (otherwise).
" Each command accepts an argument that specifies the number of spaces in a tab column. By default, the 'tabstop' setting is used.
"------------------------------------------------

" Return indent (all whitespace at start of a line), converted from
" tabs to spaces if what = 1, or from spaces to tabs otherwise.
" When converting to tabs, result has no redundant spaces.
function! Indenting(indent, what, cols)
  let spccol = repeat(' ', a:cols)
  let result = substitute(a:indent, spccol, '\t', 'g')
  let result = substitute(result, ' \+\ze\t', '', 'g')
  if a:what == 1
    let result = substitute(result, '\t', spccol, 'g')
  endif
  return result
endfunction

" Convert whitespace used for indenting (before first non-whitespace).
" what = 0 (convert spaces to tabs), or 1 (convert tabs to spaces).
" cols = string with number of columns per tab, or empty to use 'tabstop'.
" The cursor position is restored, but the cursor will be in a different
" column when the number of characters in the indent of the line is changed.
function! IndentConvert(line1, line2, what, cols)
  let savepos = getpos('.')
  let cols = empty(a:cols) ? &tabstop : a:cols
  execute a:line1 . ',' . a:line2 . 's/^\s\+/\=Indenting(submatch(0), a:what, cols)/e'
  call histdel('search', -1)
  call setpos('.', savepos)
endfunction
command! -nargs=? -range=% Space2Tab call IndentConvert(<line1>,<line2>,0,<q-args>)
command! -nargs=? -range=% Tab2Space call IndentConvert(<line1>,<line2>,1,<q-args>)
command! -nargs=? -range=% RetabIndent call IndentConvert(<line1>,<line2>,&et,<q-args>)

"------------------------------------------------

"Function to run arbitrary commands while preserving state.
"Found at <http://technotales.wordpress.com/2010/03/31/preserve-a-vim-function-that-keeps-your-state/>
function! Preserve(command)
  "Preparation: save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")
  "Do the business:
  execute a:command
  "Clean up: restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction

"Convert mixed line endings to LF-only (Unix.)
"Found at <http://vim.wikia.com/wiki/File_format>
function! ForceLF()
  :update
  :e ++ff=dos
  :setlocal ff=unix
  :w
endfunction

"NOTE - By default, <leader> is backslash.

"vimux shortcuts.
"Found at <https://raw.github.com/benmills/vimux/master/doc/vimux.txt>

"Prompt for a command to run
map <leader>rp :VimuxPromptCommand<CR>

"Run last command executed by VimuxRunCommand
map <leader>rl :VimuxRunLastCommand<CR>

"Inspect runner pane
map <leader>ri :VimuxInspectRunner<CR>

"Close all other tmux panes in current window
map <leader>rx :VimuxClosePanes<CR>

"Close vim tmux runner opened by VimuxRunCommand
map <leader>rq :VimuxCloseRunner<CR>

"Interrupt any command running in the runner pane
map <leader>rs :VimuxInterruptRunner<CR>

"Shortcuts for opening new splits.
"Found at <http://technotales.wordpress.com/2010/04/29/vim-splits-a-guide-to-doing-exactly-what-you-want/>
"Window
nmap <leader>sw<left>  :topleft  vnew<CR>
nmap <leader>sw<right> :botright vnew<CR>
nmap <leader>sw<up>    :topleft  new<CR>
nmap <leader>sw<down>  :botright new<CR>
"Buffer
nmap <leader>s<left>   :leftabove  vnew<CR>
nmap <leader>s<right>  :rightbelow vnew<CR>
nmap <leader>s<up>     :leftabove  new<CR>
nmap <leader>s<down>   :rightbelow new<CR>

"Shortcut to toggle NERDTree
nmap <leader>e :NERDTreeToggle<CR>

"Shorcut to toggle search hilighting
"Found at <http://vim.wikia.com/wiki/Highlight_all_search_pattern_matches>
noremap <leader>h :set hlsearch! hlsearch?<CR>

"Shortcut to toggle 'set list' (toggles invisible characters)
nmap <leader>l :set list!<CR>

"Shortcut to toggle line number visibility
nmap <leader>n :set number! number?<CR>

"Shortcut to toggle auto-indenting for code paste
"Found at <http://vim.wikia.com/wiki/Toggle_auto-indenting_for_code_paste>
nnoremap <leader>p :set invpaste paste?<CR>
set pastetoggle=<leader>p
set showmode

"Shortcut to toggle Gundo
nmap <leader>u :GundoToggle<CR>

"Shortcut to strip trailing whitespace
nmap <silent> <leader>$ :call Preserve("%s/\\s\\+$//e")<CR>

"Shortcut to auto-indent entire file
nmap <silent> <leader>= :call Preserve("normal gg=G")<CR>

"Shortcut to convert mixed line endings to LF-only (Unix)
nmap <leader>d :call ForceLF()<CR>
